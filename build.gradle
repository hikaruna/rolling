/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.1/userguide/java_library_plugin.html
 */

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'

apply plugin: 'maven-publish'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
apply plugin: 'com.jfrog.bintray'



// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:22.0'
    implementation 'com.google.code.findbugs:jsr305:2.0.1'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'

}

compileJava {
    sourceCompatibility JavaVersion.VERSION_1_7.toString()
    targetCompatibility JavaVersion.VERSION_1_7.toString()
}

// Your library will be referred as your_maven_group_name:library_name:version
archivesBaseName = 'rolling'
group = 'net.hikaruna'
version = '0.0.2' // library version

def utf8 = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = utf8

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options.locale = 'en_US'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        mavenPublication(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''

    key = project.hasProperty('bintray_apikey') ? bintray_apikey : ''

    pkg {
        repo = 'maven'
        name = 'rolling'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/hikaruna/rolling'
        issueTrackerUrl = 'https://github.com/hikaruna/rolling/issues'
        vcsUrl = 'https://github.com/hikaruna/rolling.git'
        version {
            name = project.version
            released = new Date()
            gpg {
                sign = true
                passphrase = project.hasProperty('bintray_gpg_password') ? bintray_gpg_password : ''
            }
        }
    }
    publications = ['mavenPublication']
    configurations = ['archives']
}
